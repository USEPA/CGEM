   SUBROUTINE Call_IOP_PAR(                                            &
		 & RadSfcIn    , sun_zenith,                           &
                 & CDOM_ij     , totChl    ,                           &
                 & OM1_A       , OM1_fp    ,                           &
                 & OM1_rp      , OM1_bc    ,                           &
                 & bottom_depth, numdepths ,                           &
	         & d_sfc       , IOPpar    ,                           &
		 & RadBotOut   , RadMidOut                            )   
   
!-----------------------------------------------------------------------
! Code is based on Brad Penta's code
!---------------------------------------------------------------------
  ! Definitions
  !  
  ! RadSfcIn     -> Downward spectrally integrated visible spectrum 
  !                 irradiance just below the sea surface. Units are 
  !                 assumed to be
  !                       quanta/cm**2/sec 
  !                 These are the units that the phytoplankton 
  !                 photosynthesis growth rate needs to be calculated 
  !                 correctly! The sfc radiation generated by NRL
  !                 appears to be in units of watts/m2/sec. The units
  !                 are converted to quanta/cm**2/sec by multiplying
  !                 R(i,j) by the parameter cv=3.021948e14 in subroutine
  !                 getSolar.  It is already multiplied by Parairsea
  !
  ! totChl       -> chlorophyll profile (all phytoplankton groups)
  !
  ! det          -> SPM- from OM1_rp, OM1_fp, OM1_A, OM1_bc
  ! 
  ! sunzenith    -> angle of the sun in radians, computed from latitude,
  !                 longitude, Julian day, and time--This is the zenith 
!------------------------------------------------------------------------------
  USE Model_dim
  USE LIGHT_VARS 

  IMPLICIT NONE 

!----------------------------------------------------------------------------
! Declare variables in the function interface. All are INTENT(IN)
! See above for physical definition of interface variables.
!----------------------------------------------------------------------------
  REAL   , INTENT(IN)    :: RadSfcIn     ! Irradiance just below sea surface
 
  REAL   , INTENT(IN)    :: sun_zenith   ! Angle of the sun

  REAL   , INTENT(IN)    :: totChl(nzp1)         !  g/m3) (total Chl-a)
                                                 !  in layer k

  REAL   , INTENT(IN)    :: CDOM_ij(nzp1)        !  CDOM (ppb)
                                                 !  in layer k

  REAL   , INTENT(IN)    :: OM1_A(nzp1)  ! Concentration of particulate
                                         ! dead phytoplankton (g/m3)
                                         ! in layer k

  REAL   , INTENT(IN)    :: OM1_fp(nzp1) ! Concentration of particulate
                                         ! fecal pellets (g/m3)
                                         ! in layer k

  REAL   , INTENT(IN)    :: OM1_rp(nzp1) ! Concentration of particulate
                                         ! river generated SPM (g/m3)
                                         ! in layer k

  REAL   , INTENT(IN)    :: OM1_bc(nzp1) ! Concentration of particulate
                                         ! initial and boundary condition generated SPM (g/m3)
                                         ! in layer k

  REAL   , INTENT(IN) :: bottom_depth(nzp1)
                                         ! depths(k) is the depth
                                         ! at the Bottom of layer k.
                                         ! it is assumed that the top
                                         ! of layer k=1 has a depth
                                         ! of zero.

  REAL   , INTENT(IN) :: d_sfc(nzp1)     ! depth at center of cell k from surface

  INTEGER, INTENT(IN)    :: numdepths    ! Total number of layers in water
                                         ! column

! IOPpar is a 1-D array of dimension nzp1. It represents the
! % of incoming surface visible irradiance, RadSfcIn, just below
! the sea surface.
  REAL  , INTENT(OUT)    :: IOPpar(nzp1)       ! IOPpar(k) is the % of
                                               ! incoming visible
                                               ! irradiance, RadSfcIn,
                                               ! at level k. RadSfcIn is
                                               ! right below the ocean sfc.

  REAL  , INTENT(OUT)    :: RadBotOut(nzp1)       ! RadBotOut(k) is Par,the
                                                  ! visible irradiance,
                                                  ! at the bottom of layer k.
                                                  ! Units are quanta/cm**2/sec
                                                  !

  REAL  , INTENT(OUT)    :: RadMidOut(nzp1)       ! RadMidOut(k) is Par,the
                                                  ! visible irradiance
                                                  ! at the middle of layer k.
                                                  ! Units are quanta/cm**2/sec

!----------------------------------------------------------------
! Calculate absorption (490 nm) components: seawater, chl, SPM from rivers, CDOM,
! detritus (dead cells), fecal pellets ...
      real Chla_tot, CDOM_tot, OM1A_tot, OM1fp_tot, OM1rp_tot, OM1bc_tot, CDOM(nzp1)
      real a490_mid, aSw_mid, aChl490_mid, aCDOM490_mid, bbChl490_mid, bb490_mid
      real a490_bot, aSw_bot, aChl490_bot, aCDOM490_bot, bbChl490_bot, bb490_bot
      real aOM1A490_mid, aOM1fp490_mid, aOM1rp490_mid, aOM1bc490_mid
      real aOM1A490_bot, aOM1fp490_bot, aOM1rp490_bot, aOM1bc490_bot
      integer :: k  

! First, convert CDOM(ppb) into CDOM, a490 (m-1)
! Once the CDOM (QSE ppb) is in the model domain, we advect and mix using the same 
! routine as for other dissolved constituents. However, to use the CDOM in the light 
! attenuation models, we need to calculate a490 (Penta et al. 2008). 
! 1) convert CDOM(QSE ppb) back to a312: a312 = (CDOM(QSE ppb)-0.538)/2.933 
! 2) convert a312 to a490: a490 = a312*exp(-0.016*(490-312)), where here S = 0.016 
! (mean value from D'Sa and DiMarco (2008)
   do k=1,numdepths
      CDOM(k) = (CDOM_ij(k) - 0.538)/2.933 !ppb to a312
      CDOM(k) = CDOM(k) * exp(-0.016*(490.-312.))
   enddo 

!Initialize counters for Chla, CDOM, and detritus:
   Chla_tot = 0.
   CDOM_tot = 0.
   OM1A_tot = 0.
   OM1fp_tot = 0.
   OM1rp_tot = 0.
   OM1bc_tot = 0.

   do k=1,numdepths
!Sum components to depth:
      Chla_tot = Chla_tot + totChl(k)
      CDOM_tot = CDOM_tot + CDOM(k)
      OM1A_tot = OM1A_tot + OM1_A(k)
      OM1fp_tot = OM1fp_tot + OM1_fp(k)
      OM1rp_tot = OM1rp_tot + OM1_rp(k)
      OM1bc_tot = OM1bc_tot + OM1_bc(k)
!Calculate absorption coefficients:
      aSw_mid = aw490 * d_sfc(k) !Sea water absorption at mid cell
      aSw_bot = aw490 * bottom_depth(k) !Sea water absorption at bottom of cell
      aChl490_mid = astar490 * Chla_tot / d_sfc(k)        !Chla absorption at mid cell
      aChl490_bot = astar490 * Chla_tot / bottom_depth(k) !Chla absorption at bottom 
      aCDOM490_mid = CDOM_tot / d_sfc(k)        !CDOM absorption at mid cell
      aCDOM490_bot = CDOM_tot / bottom_depth(k) !CDOM absorption at mid cell
      aOM1A490_mid = astarOMA * OM1A_tot / d_sfc(k) ! absorption at mid cell
      aOM1A490_bot = astarOMA * OM1A_tot / bottom_depth(k) !SPM absorption at mid cell
      aOM1fp490_mid = astarOMfp * OM1fp_tot / d_sfc(k) ! absorption at mid cell
      aOM1fp490_bot = astarOMfp * OM1fp_tot / bottom_depth(k) !SPM absorption at mid cell
      aOM1rp490_mid = astarOMrp * OM1rp_tot / d_sfc(k) ! absorption at mid cell
      aOM1rp490_bot = astarOMrp * OM1rp_tot / bottom_depth(k) !SPM absorption at mid cell
      aOM1bc490_mid = astarOMbc * OM1bc_tot / d_sfc(k) ! absorption at mid cell
      aOM1bc490_bot = astarOMbc * OM1bc_tot / bottom_depth(k) !SPM absorption at mid cell
      a490_mid = aSw_mid + aChl490_mid + aCDOM490_mid + aOM1A490_mid + aOM1fp490_mid + aOM1rp490_mid + aOM1bc490_mid
      a490_bot = aSw_bot + aChl490_bot + aCDOM490_bot + aOM1A490_bot + aOM1fp490_bot + aOM1rp490_bot + aOM1bc490_bot 
!Calculate backscattering coefficients:
      bbChl490_mid = 0.015 * (0.3*((Chla_tot / d_sfc(k))**0.62)*(550./490.)) !Chla backscatter at mid cell
      bbChl490_bot = 0.015 * (0.3*((Chla_tot / bottom_depth(k))**0.62)*(550./490.)) !Chla backscatter at bottom
      bb490_mid = bbChl490_mid !Only Chla backscatters for now
      bb490_bot = bbChl490_bot
      !write(6,*) "k=",k,a490_mid,a490_bot,bb490_mid,bb490_bot
! Calculate PAR at depth
      call IOP_PARattenuation(AMAX1(a490_mid,0.), bb490_mid, RadSfcIn, sun_zenith, d_sfc(k), RadMidOut(k)) 
      call IOP_PARattenuation(AMAX1(a490_bot,0.), bb490_bot, RadSfcIn, sun_zenith, bottom_depth(k), RadBotOut(k))
      IOPpar(k) = 100.*RadBotOut(k)/RadSfcIn
      !write(6,*) "k=",k,"IOPpar=",IOPpar(k),"RadBotOut=",RadBotOut(k)
   enddo
!stop

   END SUBROUTINE Call_IOP_PAR
!----------------------------------------------------------------------
!*****************************************************************************************
      subroutine IOP_PARattenuation(a490, bb490, PARsurf, sun_zenith, d_sfc, PARdepth)

! Penta et al., 2008 PAR penetration model: based on the IOP (inherent optical properties)
! absorption and backscatter at 490nm and the zenith angle of the sun; modified from Lee 
! et al., 2005 PAR penetration model: based on satellite absorption and backscater at 490nm

!    Lee, Z., K. Du, R. Arnone, S. Liew, and B. Penta, .Penetration of solar radiation
!         in the upper ocean: a numerical model for oceanic and coastal waters,. 
!         J. Geophys. Res. 110, C09019 doi:10.1029/2004JC002780 (2005).
!    Penta, B., Z. Lee, R.M. Kudela, S.L. Palacios, D.J. Gray, J.K. Jolliff, and 
!         I.G. Shulman, .An underwater light attenuation scheme for marine ecosystem 
!         models., Optics Express, 16, 16582-16591 (2008).

! Absorption (a490) is the total absorption at 490 nm calculated from all of the 
! constituents of the model that absorb light (Seawater, chlorophyll, CDOM, detritus, etc.
! Backscatter (bb490) is similar 

! Use the average value from the sea-surface to the depth of the calculation 
! (these calculations moved to a new subroutine(s) and the a490 and bb490 will be passed 
! into this subroutine  

! PAR (photosynthetically active radiation) just below the sea surface is used as a 
! starting value to be multiplied by the attenuation factor computed in this subroutine. 
! The starting value does not affect any of these calculations

! We generally use a factor of 0.4815 to represent the loss of PAR passing across the
! Air/Sea interface - this is calculated already in the atmospheric model

! Define coefficients for light attenuation model       
      real a490, alpha0, alpha1, alpha2, bb490, chi0, chi1, chi2, d_sfc
      real k1, k2, kpar, PARdepth, PARsurf, sun_zenith, zeta0, zeta1
      real zeta2 
     
! sun_zenith => Solar Zenith Angle in radians for time and location
! d_sfc => depth of center of cell from elevated sea surface        

! Set values of coefficients for light attenuation model 
! (Lee et al., 2005; Penta et al., 2008)       
      parameter(alpha0=0.090, alpha1=1.465, alpha2=-0.667, chi0=-0.057, chi1=0.482, & 
     &          chi2=4.221, zeta0=0.183, zeta1=0.702, zeta2=-2.567)
     
! Calculate the attenuation (Equation 2 Penta et al., 2008 errata)
      k1 = ((chi0 + chi1*(a490**0.5) + chi2*bb490) & 
     &     * (1 + alpha0 * sin(sun_zenith)))
     
      k2 = ((zeta0 + zeta1*a490 + zeta2*bb490) &
     &     * (alpha1 + alpha2 * cos(sun_zenith) ))
     
      kpar = k1 + (k2 / (1+(d_sfc))**0.5)
     
      PARdepth = PARsurf * exp(-(kpar*(d_sfc)))

      return
      end
!*****************************************************************************************
