  subroutine Brad_Light_Model(f,fm,TC_8,lat,lon,Rad,d,d_sfc,IOPpar)

  USE Model_dim
  USE DATE_TIME
  USE STATES
  USE EUT, ONLY:CCHLD, CCHLG
  USE LIGHT_VARS
  USE INPUT_VARS_GD, ONLY: Read_Solar

  IMPLICIT NONE

  real, intent(in)    :: Rad(im,jm)     !Irradiance just below sea surface
  real, intent(in)    :: f(im,jm,nsl,nf)
  integer, intent(in) :: fm(im,jm)
  real, intent(in)    :: lat(jm),lon(im)    !Latitude and longitude of each grid cell
  real, intent(out)   :: IOPpar(im,jm,nsl)  !Par at the middle of layer k
  real, intent(in)    :: d_sfc(im,jm,nsl)   !Depth at center of cell k from surface
  real, intent(in)    :: d(im,jm,nsl)   !Depth at center of cell k from surface
  real :: sun_zenith, calc_solar_zenith
  real :: totChl(nsl)  ! total Chl-a (mg/m3)
  real :: CDOM_k(nsl)  ! CDOM (ppb)
  real :: OM1_A(nsl)   ! Concentration of particulate
  real :: OM1_Z(nsl)   ! Concentration of particulate
  real :: OM1_R(nsl)   ! Concentration of particulate
  real :: OM1_BC(nsl)  ! Concentration of particulate
                       ! initial and boundary condition
                       ! generated SPM (g/m3)
  real :: PARsurf(im,jm),PAR_percent(nsl), PARbot     ! Not used in GD Light Model 
  integer(kind=8), intent(in) :: TC_8  ! Model time (seconds from beginning of Jan 1, 2002)
! Time variables
    real, parameter :: OneD60     = 1.0/60.0  ! Convert 1/min to 1/sec
    real            :: HrTC          ! Decimal hour of day
    integer         :: iYrTC, iMonTC, iDayTC, iHrTC, iMinTC, iSecTC !Time variables
    integer         :: JY            ! Function determines whether argument is a leap year
    integer         :: julianDay     ! Holds Julian Day
    integer         :: mdate_julian  ! Function calculates Julian Day
    logical         :: leapyr        ! Logical, is leap year
  integer i,j,numdepths

  numdepths = nz

 !--Begin Calculate atmospheric model --------------------------------
         ! Rad(i,j) is short wave generated by NRL is used,
         ! and is multiplied by SWtoPAR: ratio of PAR to
         ! shortwave radiation (hardcoded 4/30/14 to 0.43).
         ! Hardcoded to 0.47 on 2/11/16, Re: Tsubo and Walker, 2005
                    PARsurf = (0.47 * Rad) * PARfac

  if(Read_Solar.eq.1) PARsurf = Rad * 0.47 * 2.77e14

 ! First calculate the Julian(GMT) model year (iYrTC), month (iMonTC),
 ! day (iDayTC), hour (iHrTC), minute (iMinTC), and second (iSecTC)
 ! associated with the midpoint of the present timestep istep TC_8

      CALL DATE_TIMESTAMP( iYr0, TC_8, &
                           iYrTC, iMonTC, iDayTC, iHrTC, iMinTC, iSecTC )

 ! Calc HrTC, the decimal hour of day
       HrTC = iHrTC + OneD60*iMinTC + OneD60*iSecTC

 ! Now calculate whether or not the year iYrTC is a leap year
      if(JY(iYrTC) == 1) then
         leapyr = .FALSE.   ! iYrTC is not a leap year
      else
         leapyr = .TRUE.    ! iYrTC is     a leap year
      endif

 ! Now calculate the Julian Day associated with model time TC_8
      julianDay = mdate_julian(iMonTC,iDayTC,leapyr)

   do j = 1,jm
     do i = 1,im
       if(fm(i,j).eq.1) then

         sun_zenith = calc_solar_zenith(lat(j), lon(i),  HrTC, julianDay, leapyr)

         totChl(:) = (f(i,j,:,JDIA) * 1.0E6 / CCHLD) + (f(i,j,:,JGRE) * 1.0E6 / CCHLG)
         OM1_A(:) = 0.
         OM1_Z(:) = f(i,j,:,JZOO) * 1.0E3
         OM1_R(:) = f(i,j,:,JROC) * 1.0E3
         OM1_BC(:) = f(i,j,:,JLOC) * 1.0E3
         CDOM_k(:) = astarOMA 


         call Call_IOP_PAR(                                            &
		 & PARsurf(i,j), sun_zenith,                           &
                 & CDOM_k      , totChl    ,                           &
                 & OM1_A       , OM1_Z     ,                           &
                 & OM1_R       , OM1_BC    ,                           &
                 & d(i,j,:)    , numdepths ,                           &
	         & d_sfc(i,j,:), PAR_percent,                          &
		 & PARbot      , IOPpar(i,j,:)                         )   


       endif
     enddo
   enddo

   IOPpar = IOPpar/2.77e14  !Convert to Watts

 
   return
 
   END Subroutine Brad_Light_Model 
