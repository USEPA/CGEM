  subroutine Brad_Light_Model(f,TC_8,lat,lon,Rad,d,d_sfc,IOPpar)

  USE Model_dim
  USE DATE_TIME
  USE STATES
  USE EUT, ONLY:CCHLD, CCHLG
  USE LIGHT_VARS
  USE INPUT_VARS, ONLY: Read_Solar

  IMPLICIT NONE

  real, intent(in)    :: Rad(im,jm)     !Irradiance just below sea surface
  real, intent(in)    :: f(0:(myim+1),jm,km,nf)
  real, intent(in)    :: lat(im,jm),lon(im,jm)    !Latitude and longitude of each grid cell
  real, intent(out)   :: IOPpar(im,jm,km)  !Par at the middle of layer k
  real, intent(in)    :: d_sfc(im,jm,km)   !Depth at center of cell k from surface
  real, intent(in)    :: d(im,jm,km)   !Depth at center of cell k from surface
  real :: sun_zenith, calc_solar_zenith 
  real :: totChl(km)  ! total Chl-a (mg/m3)
  real :: CDOM_k(km)  ! CDOM (ppb)
  real :: OM1_A(km)   ! Concentration of particulate
  real :: OM1_Z(km)   ! Concentration of particulate
  real :: OM1_R(km)   ! Concentration of particulate
  real :: OM1_BC(km)  ! Concentration of particulate
                       ! initial and boundary condition
                       ! generated SPM (g/m3)
  real :: PARsurf(im,jm),PAR_percent(km), PARbot     ! Not used in WQEM Light Model 
  integer(kind=8), intent(in) :: TC_8  ! Model time (seconds from beginning of Jan 1, 2002)
! Time variables
    real, parameter :: OneD60     = 1.0/60.0  ! Convert 1/min to 1/sec
    real            :: HrTC          ! Decimal hour of day
    integer         :: iYrTC, iMonTC, iDayTC, iHrTC, iMinTC, iSecTC !Time variables
    integer         :: julianDay     ! Holds Julian Day
  integer i,j,nz,numdepths,myi


 !--Begin Calculate atmospheric model --------------------------------

 ! Rad(i,j) is short wave generated by NRL is used,
 ! and is multiplied by SWtoPAR: ratio of PAR to
 ! shortwave radiation (hardcoded 4/30/14 to 0.43).
 ! Hardcoded to 0.47 on 2/11/16, Re: Tsubo and Walker, 2005
  PARsurf = (0.47 * Rad) * PARfac

  if(Read_Solar.eq.2) PARsurf = Rad*2.77e14/4.57 * PARfac 

 ! First calculate the Julian(GMT) model year (iYrTC), month (iMonTC),
 ! day (iDayTC), hour (iHrTC), minute (iMinTC), and second (iSecTC)
 ! associated with the midpoint of the present timestep istep TC_8

  CALL DATE_TIMESTAMP( iYr0, TC_8, iYrTC, iMonTC, iDayTC, iHrTC, iMinTC, iSecTC )

 ! Calc HrTC, the decimal hour of day
       HrTC = real(iHrTC,4) + OneD60*iMinTC + OneD60*iSecTC

! Now calculate the Julian Day associated with model time TC_8
      julianDay = JDAY_IN_YEAR(iYrTC, iMonTC, iDayTC)

   do j = 1, jm
     myi = 1
     do i = myi_start, myi_end
        nz = nza(i,j)
        if(nz.gt.0) then

         sun_zenith = calc_solar_zenith(lat(i,j),lon(i,j),HrTC,julianDay)

         totChl(1:nz) = (f(myi,j,1:nz,JDIA) * 1.0E6 / CCHLD) + (f(myi,j,1:nz,JGRE) * 1.0E6 / CCHLG)
         OM1_A(1:nz) = 0.
         OM1_Z(1:nz) = f(myi,j,1:nz,JZOO) * 1.0E3
         OM1_R(1:nz) = f(myi,j,1:nz,JROC) * 1.0E3
         OM1_BC(1:nz) = f(myi,j,1:nz,JLOC) * 1.0E3
         CDOM_k(1:nz) = astarOMA 
         numdepths = nz

         call Call_IOP_PAR(                                            &
		 & PARsurf(i,j), sun_zenith,                           &
                 & CDOM_k      , totChl    ,                           &
                 & OM1_A       , OM1_Z     ,                           &
                 & OM1_R       , OM1_BC    ,                           &
                 & d(i,j,1:nz)    , numdepths ,                           &
	         & d_sfc(i,j,1:nz), PAR_percent,                          &
		 & PARbot      , IOPpar(i,j,1:nz)                         )   


       endif
       myi = myi + 1
     enddo
   enddo

   IOPpar = IOPpar/2.77e14  !Convert to Watts/m2/s
   IOPpar = IOPpar*4.57     !Convert to umol/m2/s
 
   return
 
   END Subroutine Brad_Light_Model 
